create or replace package OBJECT_TO_JSON
AUTHID CURRENT_USER
as
--
$IF DBMS_DB_VERSION.VER_LE_11_2 $THEN
--
$ELSE
--
  TYPE TABLE_INFO_RECORD is RECORD (
    OWNER      VARCHAR2(128)
   ,TABLE_NAME VARCHAR2(128)
   );  

  TYPE TABLE_INFO_TABLE is TABLE of TABLE_INFO_RECORD;
  
  TYPE TYPE_LIST is RECORD (
    OWNER               VARCHAR2(128)
  , TYPE_NAME           VARCHAR2(128)
  , ATTR_COUNT          NUMBER
  , TYPECODE            VARCHAR2(32)
  );

  TYPE TYPE_LIST_TABLE is TABLE of TYPE_LIST;
--
$END
--  
  function SERIALIZE_TYPE(P_TYPE_OWNER VARCHAR2, P_TYPE_NAME VARCHAR2) return CLOB;
  function SERIALIZE_TABLE_TYPES(P_TABLE_OWNER VARCHAR2, P_TABLE_NAME VARCHAR2) return CLOB;
  function SERIALIZE_TABLE_TYPES(P_TABLE_LIST TABLE_INFO_TABLE) return CLOB;

  function DESERIALIZE_TYPE(P_TYPE_OWNER VARCHAR2, P_TYPE_NAME VARCHAR2) return VARCHAR2;
  function DESERIALIZE_TABLE_TYPES(P_TABLE_OWNER VARCHAR2, P_TABLE_NAME VARCHAR2) return CLOB;
  function DESERIALIZE_TABLE_TYPES(P_TABLE_LIST TABLE_INFO_TABLE) return CLOB;
  procedure DESERIALIZE_TYPE(P_TYPE_OWNER VARCHAR2, P_TYPE_NAME VARCHAR2, P_PLSQL_BLOCK IN OUT NOCOPY CLOB);
  procedure DESERIALIZE_TABLE_TYPES(P_TABLE_OWNER VARCHAR2, P_TABLE_NAME VARCHAR2, P_PLSQL_BLOCK IN OUT NOCOPY CLOB);
  procedure DESERIALIZE_TABLE_TYPES(P_TABLE_LIST TABLE_INFO_TABLE, P_PLSQL_BLOCK IN OUT NOCOPY CLOB);

  function SERIALIZE_BFILE(P_BFILE BFILE) return VARCHAR2;
  function DESERIALIZE_BFILE(P_SERIALIZATION VARCHAR2) return BFILE;
  function SERIALIZE_ANYDATA(P_ANYDATA ANYDATA) return CLOB;

  procedure APPEND_BLOB_AS_HEX(P_BLOB BLOB, P_SERIALIZATION IN OUT NOCOPY CLOB);
end;
/
--
set TERMOUT on
--
show errors
--
@@SET_TERMOUT
--
create or replace package body OBJECT_TO_JSON
as
--
  C_TYPED_JSON             BOOLEAN := TRUE;
  C_SERIALIZE_OBJECT_PART1 VARCHAR2(32767) := 
--
'procedure SERIALIZE_OBJECT(P_TABLE_OWNER VARCHAR2,P_ANYDATA ANYDATA, P_SERIALIZATION IN OUT NOCOPY CLOB)
as
  V_DOUBLE_QUOTE        CONSTANT CHAR(1) := ''"'';
  TYPE TYPE_INFO_T is RECORD (
    TYPE_ID             NUMBER
   ,PRECISION           NUMBER
   ,SCALE               NUMBER
   ,length              NUMBER
   ,CSID                NUMBER
   ,CSFRM               NUMBER
   ,OWNER               VARCHAR2(128)
   ,TYPE_NAME           VARCHAR2(128)
   ,TYPE_VERSION        VARCHAR2(128)
   ,ATTR_COUNT          NUMBER
  );
  TYPE ATTR_INFO_T IS RECORD (
    TYPE_ID             NUMBER
   ,PRECISION           NUMBER
   ,SCALE               NUMBER
   ,length              NUMBER
   ,CSID                NUMBER
   ,CSFRM               NUMBER
   ,ATTR_TYPE_METADATA  ANYTYPE
   ,ATTR_NAME           VARCHAR2(128)
  );
  V_TYPE_ID             PLS_INTEGER;
  V_ANYDATA             ANYDATA;
  V_TYPE_METADATA       ANYTYPE;
  V_TYPE_INFO           TYPE_INFO_T;
  V_ATTR_INFO           ATTR_INFO_T;
  V_RESULT              PLS_INTEGER;
  V_OBJECT_CONSTRUCTOR  VARCHAR2(266);
  V_SERIALIZED_VALUE    VARCHAR2(266);
begin
  V_TYPE_ID := P_ANYDATA.getType(V_TYPE_METADATA);
  V_TYPE_INFO.TYPE_ID := V_TYPE_METADATA.getInfo(
    V_TYPE_INFO.PRECISION,
    V_TYPE_INFO.SCALE,
    V_TYPE_INFO.length,
    V_TYPE_INFO.CSID,
    V_TYPE_INFO.CSFRM,
    V_TYPE_INFO.OWNER,
    V_TYPE_INFO.TYPE_NAME,
    V_TYPE_INFO.TYPE_VERSION,
    V_TYPE_INFO.ATTR_COUNT
  );
  case
';
--
  C_SERIALIZE_OBJECT CONSTANT VARCHAR(1024) :=
--
'function SERIALIZE_OBJECT(P_TABLE_OWNER VARCHAR2,P_ANYDATA ANYDATA)
return CLOB
as
  V_SERIALIZATION    CLOB;
begin
  DBMS_LOB.CREATETEMPORARY(V_SERIALIZATION,TRUE,DBMS_LOB.CALL);
  SERIALIZE_OBJECT(P_TABLE_OWNER,P_ANYDATA,V_SERIALIZATION);
  return V_SERIALIZATION;
end;
';
--
function CODE_SERIALIZE_OBJECT return VARCHAR2 deterministic
as
begin
  return C_SERIALIZE_OBJECT;
end;
--
function CODE_SERIALIZE_OBJECT_PART1 return VARCHAR2 deterministic
as
begin
  return C_SERIALIZE_OBJECT_PART1;
end;
--
function SERIALIZE_BFILE(P_BFILE BFILE) return VARCHAR2
as
  V_DOUBLE_QUOTE     CONSTANT CHAR(1) := '"';
  V_DIRECTORY_ALIAS  VARCHAR2(128 CHAR);
  V_PATH2FILE        VARCHAR2(2000 CHAR);
begin
  DBMS_LOB.FILEGETNAME(P_BFILE,V_DIRECTORY_ALIAS,V_PATH2FILE);
  return '{ "alias": "' || V_DIRECTORY_ALIAS || '", "path": "' || V_PATH2FILE || '"}';
end;
--
function DESERIALIZE_BFILE(P_SERIALIZATION VARCHAR2) 
return BFILE
as
  V_BFILE BFILE := NULL;
begin
 
  if (P_SERIALIZATION is not NULL) then
    $IF YADAMU_FEATURE_DETECTION.JSON_PARSING_SUPPORTED $THEN
    SELECT BFILENAME(ALIAS,FILENAME) 
	  INTO V_BFILE
	  FROM JSON_TABLE(P_SERIALIZATION, '$' COLUMNS ALIAS VARCHAR2(2048) PATH '$.alias', FILENAME VARCHAR2(2048) PATH '$.path');
	$ELSE
	-- TODO 11g Parse using REGEX ????
	NULL;
	$END
  end if;
  return V_BFILE;
end;
--
procedure SERIALIZE_ANYDATA(P_ANYDATA ANYDATA, P_SERIALIZATION IN OUT NOCOPY CLOB)
as
  V_DOUBLE_QUOTE         CONSTANT CHAR(1) := '"';
  TYPE TYPE_INFO_T is RECORD (
    TYPE_ID              NUMBER
   ,PRECISION            NUMBER
   ,SCALE                NUMBER
   ,length               NUMBER
   ,CSID                 NUMBER
   ,CSFRM                NUMBER
   ,SCHEMA_NAME          VARCHAR2(128)
   ,TYPE_NAME            VARCHAR2(128)
   ,TYPE_VERSION         VARCHAR2(128)
   ,ATTR_COUNT           NUMBER
  );
  V_TYPE_ID              NUMBER;
  V_TYPE_METADATA        ANYTYPE;
  V_TYPE_INFO            TYPE_INFO_T;
  V_OBJECT_CONSTRUCTOR   VARCHAR2(512);
  V_ANYDATA_CONTENT_TYPE VARCHAR2(512);
  V_RESULT               PLS_INTEGER;
  V_ANYDATA_PAYLOAD      CLOB;
  V_CLOB                 CLOB;
  V_BLOB                 BLOB;
  V_NCLOB                NCLOB;
  V_BFILE                BFILE;
  V_BDOUBLE              BINARY_DOUBLE;
  V_BFLOAT               BINARY_FLOAT;
  V_NUMBER               NUMBER;
  V_CHAR                 CHAR(4000);
  V_NCHAR                NCHAR(4000);
  V_VARCHAR              VARCHAR(32767);
  V_VARCHAR2             VARCHAR2(32767);
  V_NVARCHAR2            NVARCHAR2(32767);
  V_RAW                  RAW(32767);
  V_DATE                 DATE;
  V_INTERVAL_DS          INTERVAL DAY TO SECOND;
  V_INTERVAL_YM          INTERVAL YEAR TO MONTH;
  V_TIMESTAMP            TIMESTAMP;
  V_TIMESTAMP_TZ         TIMESTAMP WITH TIME ZONE;
  V_TIMESTAMP_LTZ        TIMESTAMP WITH LOCAL TIME ZONE;
  V_UROWID               UROWID;
begin
  if (P_ANYDATA is NULL) then
    DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,4,'NULL`');
    return;
  end if;
  V_TYPE_ID := P_ANYDATA.getType(V_TYPE_METADATA);
  V_TYPE_INFO.TYPE_ID := V_TYPE_METADATA.getInfo(
    V_TYPE_INFO.PRECISION,
    V_TYPE_INFO.SCALE,
    V_TYPE_INFO.length,
    V_TYPE_INFO.CSID,
    V_TYPE_INFO.CSFRM,
    V_TYPE_INFO.SCHEMA_NAME,
    V_TYPE_INFO.TYPE_NAME,
    V_TYPE_INFO.TYPE_VERSION,
    V_TYPE_INFO.ATTR_COUNT
  );
  V_OBJECT_CONSTRUCTOR := '"SYS"."ANYDATA"(';
  V_ANYDATA_CONTENT_TYPE := V_DOUBLE_QUOTE || V_TYPE_INFO.SCHEMA_NAME || V_DOUBLE_QUOTE;
  case V_TYPE_ID
    when DBMS_TYPES.TYPECODE_BDOUBLE then
      V_RESULT := P_ANYDATA.getBDouble(V_BDOUBLE);
	  V_ANYDATA_PAYLOAD := TO_CHAR(V_BDOUBLE);
    when DBMS_TYPES.TYPECODE_BFILE then
      V_RESULT := P_ANYDATA.getBFile(V_BFILE);
	  V_ANYDATA_PAYLOAD := OBJECT_TO_JSON.SERIALIZE_BFILE(V_BFILE);
	when DBMS_TYPES.TYPECODE_BFLOAT then
      V_RESULT := P_ANYDATA.getBFloat(V_BFLOAT);
	  V_ANYDATA_PAYLOAD := TO_CHAR(V_BFLOAT);
    when DBMS_TYPES.TYPECODE_BLOB then
	  V_RESULT := P_ANYDATA.getBLOB(V_BLOB);
      DBMS_LOB.CREATETEMPORARY(V_ANYDATA_PAYLOAD,TRUE,DBMS_LOB.CALL);
      DBMS_LOB.WRITEAPPEND(V_ANYDATA_PAYLOAD,1,V_DOUBLE_QUOTE);
	     DBMS_LOB.APPEND(V_ANYDATA_PAYLOAD,OBJECT_SERIALIZATION.SERIALIZE_BLOB_HEX(V_BLOB));
	     DBMS_LOB.WRITEAPPEND(V_ANYDATA_PAYLOAD,1,V_DOUBLE_QUOTE);
    when DBMS_TYPES.TYPECODE_CHAR then
      V_RESULT := P_ANYDATA.getCHAR(V_CHAR);
    V_ANYDATA_PAYLOAD :=  V_DOUBLE_QUOTE || V_CHAR || V_DOUBLE_QUOTE;
    when DBMS_TYPES.TYPECODE_CLOB then
	  V_RESULT := P_ANYDATA.getCLOB(V_CLOB);
      DBMS_LOB.CREATETEMPORARY(V_ANYDATA_PAYLOAD,TRUE,DBMS_LOB.CALL);
	     DBMS_LOB.WRITEAPPEND(V_ANYDATA_PAYLOAD,1,V_DOUBLE_QUOTE);
	     DBMS_LOB.APPEND(V_ANYDATA_PAYLOAD,V_CLOB);
	     DBMS_LOB.WRITEAPPEND(V_ANYDATA_PAYLOAD,1,V_DOUBLE_QUOTE);
    when DBMS_TYPES.TYPECODE_DATE then
      V_RESULT := P_ANYDATA.getDate(V_DATE);
	  V_ANYDATA_PAYLOAD := TO_CHAR(V_DATE);
    when DBMS_TYPES.TYPECODE_INTERVAL_DS then
      V_RESULT := P_ANYDATA.getIntervalDS(V_INTERVAL_DS);
	  V_ANYDATA_PAYLOAD := TO_CHAR(V_INTERVAL_DS);
    when DBMS_TYPES.TYPECODE_INTERVAL_YM then
      V_RESULT := P_ANYDATA.getIntervalYM(V_INTERVAL_YM);
	  V_ANYDATA_PAYLOAD := TO_CHAR(V_INTERVAL_YM);
    when DBMS_TYPES.TYPECODE_NCHAR then
      V_RESULT := P_ANYDATA.getNCHAR(V_NCHAR);
      V_ANYDATA_PAYLOAD :=  V_DOUBLE_QUOTE || TO_CHAR(V_NCHAR) || V_DOUBLE_QUOTE;
    when DBMS_TYPES.TYPECODE_NCLOB then
      V_RESULT := P_ANYDATA.getNCLOB(V_NCLOB);
      DBMS_LOB.CREATETEMPORARY(V_ANYDATA_PAYLOAD,TRUE,DBMS_LOB.CALL);
	     DBMS_LOB.WRITEAPPEND(V_ANYDATA_PAYLOAD,1,V_DOUBLE_QUOTE);
	     DBMS_LOB.APPEND(V_ANYDATA_PAYLOAD,TO_CLOB(V_NCLOB));
	     DBMS_LOB.WRITEAPPEND(V_ANYDATA_PAYLOAD,1,V_DOUBLE_QUOTE);
    when DBMS_TYPES.TYPECODE_NUMBER then
      V_RESULT := P_ANYDATA.getNumber(V_NUMBER);
	  V_ANYDATA_PAYLOAD := TO_CHAR(V_NUMBER);
    when DBMS_TYPES.TYPECODE_NVARCHAR2 then
      V_RESULT := P_ANYDATA.getNVarchar2(V_NVARCHAR2);
      V_ANYDATA_PAYLOAD :=  V_DOUBLE_QUOTE || TO_CHAR(V_NVARCHAR2) || V_DOUBLE_QUOTE;
    when DBMS_TYPES.TYPECODE_RAW then
	  V_RESULT := P_ANYDATA.getRaw(V_RAW);
      V_ANYDATA_PAYLOAD :=  V_DOUBLE_QUOTE || TO_CHAR(V_RAW) || V_DOUBLE_QUOTE;
    when DBMS_TYPES.TYPECODE_TIMESTAMP then
      V_RESULT := P_ANYDATA.getTimestamp(V_TIMESTAMP);
      V_ANYDATA_PAYLOAD :=  V_DOUBLE_QUOTE || TO_CHAR(V_TIMESTAMP) || V_DOUBLE_QUOTE;
    when DBMS_TYPES.TYPECODE_TIMESTAMP_LTZ then
      V_RESULT := P_ANYDATA.getTimestampLTZ(V_TIMESTAMP_LTZ);
      V_ANYDATA_PAYLOAD :=  V_DOUBLE_QUOTE || TO_CHAR(V_TIMESTAMP_LTZ) || V_DOUBLE_QUOTE;
    when DBMS_TYPES.TYPECODE_TIMESTAMP_TZ then
      V_RESULT := P_ANYDATA.getTimestampLTZ(V_TIMESTAMP_TZ);
      V_ANYDATA_PAYLOAD :=  V_DOUBLE_QUOTE || TO_CHAR(V_TIMESTAMP_TZ) || V_DOUBLE_QUOTE;
    when DBMS_TYPES.TYPECODE_UROWID then
      -- V_RESULT := P_ANYDATA.getROWID(V_ROWID);
	  V_UROWID := P_ANYDATA.accessUROWID();
      V_ANYDATA_PAYLOAD :=  V_DOUBLE_QUOTE || TO_CHAR(V_UROWID) || V_DOUBLE_QUOTE;
    when DBMS_TYPES.TYPECODE_VARCHAR2 then
      V_RESULT := P_ANYDATA.getVARCHAR2(V_VARCHAR2);
      V_ANYDATA_PAYLOAD :=  V_DOUBLE_QUOTE || V_VARCHAR2 || V_DOUBLE_QUOTE;
    when DBMS_TYPES.TYPECODE_VARCHAR then
      V_RESULT := P_ANYDATA.getVARCHAR(V_VARCHAR);
      V_ANYDATA_PAYLOAD :=  V_DOUBLE_QUOTE || V_VARCHAR || V_DOUBLE_QUOTE;
    when 3 then -- INTERGER used in ORDSYS.ORDIMAGE
      V_ANYDATA_PAYLOAD := 'Unsupported ANYDATA content ['|| V_TYPE_ID || ']: INTEGER.';
    when DBMS_TYPES.TYPECODE_CFILE then
      V_ANYDATA_PAYLOAD := 'Unsupported ANYDATA content ['|| V_TYPE_ID || ']: CFILE.';
   when DBMS_TYPES.TYPECODE_MLSLABEL then
      V_ANYDATA_PAYLOAD := 'Unsupported ANYDATA content ['|| V_TYPE_ID || ']: MSLABEL.';
    when DBMS_TYPES.TYPECODE_NAMEDCOLLECTION then
      V_ANYDATA_CONTENT_TYPE := V_DOUBLE_QUOTE || '"' || V_TYPE_INFO.SCHEMA_NAME || '"."' || V_TYPE_INFO.TYPE_NAME || '"' || V_DOUBLE_QUOTE;
      V_ANYDATA_PAYLOAD := 'Unsupported ANYDATA content ['|| V_TYPE_ID || ']: NAMEDCOLLECTION.';
    when DBMS_TYPES.TYPECODE_OBJECT then
      V_ANYDATA_CONTENT_TYPE := V_DOUBLE_QUOTE || '"' || V_TYPE_INFO.SCHEMA_NAME || '"."' || V_TYPE_INFO.TYPE_NAME || '"' || V_DOUBLE_QUOTE;
      V_ANYDATA_PAYLOAD := 'Unsupported ANYDATA content ['|| V_TYPE_ID || ']: OBJECT.';
    when DBMS_TYPES.TYPECODE_OPAQUE then
      V_ANYDATA_PAYLOAD := 'Unsupported ANYDATA content ['|| V_TYPE_ID || ']: OPAQUE.';
    when DBMS_TYPES.TYPECODE_REF then
      V_ANYDATA_PAYLOAD := 'Unsupported ANYDATA content ['|| V_TYPE_ID || ']: REF.';
    when DBMS_TYPES.TYPECODE_TABLE then
      V_ANYDATA_CONTENT_TYPE := V_DOUBLE_QUOTE || '"' || V_TYPE_INFO.SCHEMA_NAME || '"."' || V_TYPE_INFO.TYPE_NAME || '"' || V_DOUBLE_QUOTE;
      V_ANYDATA_PAYLOAD := 'Unsupported ANYDATA content ['|| V_TYPE_ID || ']: TABLE.';
    when DBMS_TYPES.TYPECODE_VARRAY then
      V_ANYDATA_CONTENT_TYPE := V_DOUBLE_QUOTE || '"' || V_TYPE_INFO.SCHEMA_NAME || '"."' || V_TYPE_INFO.TYPE_NAME || '"' || V_DOUBLE_QUOTE;
      V_ANYDATA_PAYLOAD := 'Unsupported ANYDATA content ['|| V_TYPE_ID || ']: VARRAY.';
	else
	  V_ANYDATA_PAYLOAD := 'Unsupported ANYDATA content ['|| V_TYPE_ID || ']: UNKNOWN.';
  end case;
  DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_ANYDATA_CONTENT_TYPE),V_ANYDATA_CONTENT_TYPE);
  DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,',');
  DBMS_LOB.APPEND(P_SERIALIZATION,V_ANYDATA_PAYLOAD);
  DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,')');
end;
--
function SERIALIZE_ANYDATA(P_ANYDATA ANYDATA) 
return CLOB
as
  V_SERIALIZATION    CLOB;
begin
  DBMS_LOB.CREATETEMPORARY(V_SERIALIZATION,TRUE,DBMS_LOB.CALL);
  SERIALIZE_ANYDATA(P_ANYDATA,V_SERIALIZATION);
  return V_SERIALIZATION;
end;
--
procedure APPEND_BLOB_AS_HEX(P_BLOB BLOB, P_SERIALIZATION IN OUT NOCOPY CLOB)
is
  V_BLOB_SIZE      NUMBER := DBMS_LOB.GETLENGTH(P_BLOB);
  V_OFFSET         INTEGER := 1;
  V_AMOUNT         INTEGER := 2000;
  V_RAW_CONTENT    RAW(2000);
begin
  while (V_OFFSET <= V_BLOB_SIZE) loop
    V_AMOUNT := 2000;
    DBMS_LOB.READ(P_BLOB,V_AMOUNT,V_OFFSET,V_RAW_CONTENT);
	V_OFFSET := V_OFFSET + V_AMOUNT;
    DBMS_LOB.APPEND(P_SERIALIZATION,TO_CLOB(RAWTOHEX(V_RAW_CONTENT)));
  end loop;
end;
--
function extendTypeList(P_TYPE_LIST IN OUT NOCOPY TYPE_LIST_TABLE, P_OWNER VARCHAR2, P_TYPE_NAME VARCHAR2)
return VARCHAR2
as
  V_COUNT     NUMBER;
  V_TYPECODE  VARCHAR2(32);
  
  
  cursor getTypeHeirachy
  is
  select OWNER, TYPE_NAME, ATTRIBUTES, TYPECODE
    from ALL_TYPES at
   start with at.TYPE_NAME = P_TYPE_NAME
          and at.OWNER = P_OWNER
                  connect by prior at.TYPE_NAME = SUPERTYPE_NAME
                         and prior at.OWNER = SUPERTYPE_OWNER;

  
begin
$IF DBMS_DB_VERSION.VER_LE_11_2 $THEN
  -- ### AVOID ORA-21700 with TABLE operator and Lin 11.2
  if (P_TYPE_LIST.count > 0) then
    for i in P_TYPE_LIST.first .. P_TYPE_LIST.last loop
      if ((P_TYPE_LIST(i).OWNER = P_OWNER) and (P_TYPE_LIST(i).TYPE_NAME = P_TYPE_NAME)) then
        return P_TYPE_LIST(i).TYPECODE;
      end if;
    end loop;
  end if;
$ELSE
  begin
    select TYPECODE
      into V_TYPECODE
      from TABLE(P_TYPE_LIST)
     where OWNER = P_OWNER and TYPE_NAME = P_TYPE_NAME;
    return V_TYPECODE;
  exception
    when NO_DATA_FOUND then
      NULL;
    when OTHERS then
      RAISE;
  end;
$END

  /*
  **
  ** Need to add the Type Heirachy for the selected Type.
  **
  */

  V_TYPECODE := NULL;  
  $IF $$DEBUG $THEN DBMS_OUTPUT.PUT_LINE('extendTypeList(): Processing "' || P_OWNER || '"."' || P_TYPE_NAME || '"'); $END
  for t in getTypeHeirachy() loop 
    $IF $$DEBUG $THEN DBMS_OUTPUT.PUT_LINE('extendTypeList(): Adding "' || t.OWNER || '"."' || t.TYPE_NAME || '": Type = "' || t.TYPECODE || '". Type count = ' || t.ATTRIBUTES); $END
    if (V_TYPECODE is NULL) then
	  V_TYPECODE := t.TYPECODE;
	end if;
    P_TYPE_LIST.extend();
    P_TYPE_LIST(P_TYPE_LIST.count).OWNER := t.OWNER;
    P_TYPE_LIST(P_TYPE_LIST.count).TYPE_NAME := t.TYPE_NAME;
    P_TYPE_LIST(P_TYPE_LIST.count).ATTR_COUNT := t.ATTRIBUTES;
    P_TYPE_LIST(P_TYPE_LIST.count).TYPECODE := t.TYPECODE;
	
  end loop;
  return V_TYPECODE;
end;
--
function serializeAttr(P_ATTR_NAME VARCHAR2, P_ATTR_TYPE_OWNER VARCHAR2, P_ATTR_TYPE_NAME VARCHAR2, P_ATTR_TYPE_MOD VARCHAR2, P_TYPE_LIST IN OUT NOCOPY TYPE_LIST_TABLE)
return VARCHAR2
as
  V_PLSQL VARCHAR2(32767);
  V_TYPECODE VARCHAR2(32);
  V_KEY_NAME VARCHAR2(128);
begin
  if (P_ATTR_NAME <> 'V_OBJECT(IDX)') then
    V_KEY_NAME := SUBSTR(P_ATTR_NAME,INSTR(P_ATTR_NAME,'."')+1);
    V_PLSQL := '        DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,' || (LENGTH(V_KEY_NAME) + 2) || + ',''' || V_KEY_NAME || ': '');' || YADAMU_UTILITIES.C_NEWLINE;
  end if;

  V_PLSQL := V_PLSQL
          || '        if (' || P_ATTR_NAME || ' is NULL) then' || YADAMU_UTILITIES.C_NEWLINE
          || '          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,4,''null'');' || YADAMU_UTILITIES.C_NEWLINE
          || '        else' || YADAMU_UTILITIES.C_NEWLINE;

  case
    when P_ATTR_TYPE_OWNER is NULL then
      case
        when P_ATTR_TYPE_NAME = 'BINARY DOUBLE' then
          V_PLSQL := V_PLSQL
                  || '         V_SERIALIZED_VALUE := TO_CHAR(' || P_ATTR_NAME || ');' || YADAMU_UTILITIES.C_NEWLINE
                  || '         DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_SERIALIZED_VALUE),V_SERIALIZED_VALUE);' || YADAMU_UTILITIES.C_NEWLINE;
        when P_ATTR_TYPE_NAME = 'BFILE' then
          V_PLSQL := V_PLSQL
                  ||'          V_SERIALIZED_VALUE := OBJECT_TO_JSON.SERIALIZE_BFILE(' || P_ATTR_NAME || ');' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_SERIALIZED_VALUE),V_SERIALIZED_VALUE);' || YADAMU_UTILITIES.C_NEWLINE;
        when P_ATTR_TYPE_NAME = 'BINARY FLOAT' then
          V_PLSQL := V_PLSQL
                  ||'          V_SERIALIZED_VALUE := TO_CHAR(' || P_ATTR_NAME || ');' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_SERIALIZED_VALUE),V_SERIALIZED_VALUE);' || YADAMU_UTILITIES.C_NEWLINE;
        when P_ATTR_TYPE_NAME = 'BLOB' then
          V_PLSQL := V_PLSQL
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,V_DOUBLE_QUOTE);' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          OBJECT_TO_JSON.APPEND_BLOB_AS_HEX(' || P_ATTR_NAME || ',P_SERIALIZATION);' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,V_DOUBLE_QUOTE);' || YADAMU_UTILITIES.C_NEWLINE;
        --  when P_ATTR_TYPE_NAME = CFILE then
        when P_ATTR_TYPE_NAME = 'CHAR' then
          V_PLSQL := V_PLSQL
                  ||'          V_SERIALIZED_VALUE := V_DOUBLE_QUOTE || ' || P_ATTR_NAME || ' || V_DOUBLE_QUOTE;' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_SERIALIZED_VALUE),V_SERIALIZED_VALUE);' || YADAMU_UTILITIES.C_NEWLINE;
        when P_ATTR_TYPE_NAME = 'CLOB' then
          V_PLSQL := V_PLSQL
                  ||'          DBMS_LOB.APPEND(P_SERIALIZATION,OBJECT_SERIALIZATION.CLOB2CHUNKS(' || P_ATTR_NAME || '));' || YADAMU_UTILITIES.C_NEWLINE;
        when P_ATTR_TYPE_NAME = 'DATE' then
          V_PLSQL := V_PLSQL
                  ||'          V_SERIALIZED_VALUE := V_DOUBLE_QUOTE || TO_CHAR(' || P_ATTR_NAME || ') || V_DOUBLE_QUOTE;' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_SERIALIZED_VALUE),V_SERIALIZED_VALUE);' || YADAMU_UTILITIES.C_NEWLINE;
        when P_ATTR_TYPE_NAME = 'INTERVAL DAY TO SECOND' then
          V_PLSQL := V_PLSQL
                  ||'          V_SERIALIZED_VALUE := V_DOUBLE_QUOTE || TO_CHAR(' || P_ATTR_NAME || ') || V_DOUBLE_QUOTE;' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_SERIALIZED_VALUE),V_SERIALIZED_VALUE);' || YADAMU_UTILITIES.C_NEWLINE;
        when P_ATTR_TYPE_NAME = 'INTEGER' then
          V_PLSQL := V_PLSQL
                  ||'          V_SERIALIZED_VALUE := TO_CHAR(' || P_ATTR_NAME || ');' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_SERIALIZED_VALUE),V_SERIALIZED_VALUE);' || YADAMU_UTILITIES.C_NEWLINE;
        when P_ATTR_TYPE_NAME = 'INTERVAL YEAR TO MONTH' then
          V_PLSQL := V_PLSQL
                  ||'          V_SERIALIZED_VALUE := V_DOUBLE_QUOTE || TO_CHAR(' || P_ATTR_NAME || ') || V_DOUBLE_QUOTE;' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_SERIALIZED_VALUE),V_SERIALIZED_VALUE);' || YADAMU_UTILITIES.C_NEWLINE;
        --  when P_ATTR_TYPE_NAME = MLSLABEL then
        when P_ATTR_TYPE_NAME = 'NCHAR' then
          V_PLSQL := V_PLSQL
                  ||'          V_SERIALIZED_VALUE := TO_CHAR(' || P_ATTR_NAME || ');' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,V_DOUBLE_QUOTE);' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_SERIALIZED_VALUE),V_SERIALIZED_VALUE);' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,V_DOUBLE_QUOTE);' || YADAMU_UTILITIES.C_NEWLINE;
        when P_ATTR_TYPE_NAME = 'NCLOB' then
          V_PLSQL := V_PLSQL
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,V_DOUBLE_QUOTE);' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.APPEND(P_SERIALIZATION,TO_CLOB(' || P_ATTR_NAME || '));' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,V_DOUBLE_QUOTE);' || YADAMU_UTILITIES.C_NEWLINE;
        when P_ATTR_TYPE_NAME = 'NUMBER' then
          V_PLSQL := V_PLSQL
                  ||'          V_SERIALIZED_VALUE := TO_CHAR(' || P_ATTR_NAME || ');' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_SERIALIZED_VALUE),V_SERIALIZED_VALUE);' || YADAMU_UTILITIES.C_NEWLINE;
        when P_ATTR_TYPE_NAME = 'NVARCHAR2' then
          V_PLSQL := V_PLSQL
                  ||'          V_SERIALIZED_VALUE := TO_CHAR(' || P_ATTR_NAME || ');' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,V_DOUBLE_QUOTE);' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_SERIALIZED_VALUE),V_SERIALIZED_VALUE);' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,V_DOUBLE_QUOTE);' || YADAMU_UTILITIES.C_NEWLINE;
        --  when P_ATTR_TYPE_NAME = OPAQUE then
        when P_ATTR_TYPE_NAME = 'RAW' then
          V_PLSQL := V_PLSQL
                  ||'          V_SERIALIZED_VALUE := TO_CHAR(' || P_ATTR_NAME || ');' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_SERIALIZED_VALUE),V_SERIALIZED_VALUE);' || YADAMU_UTILITIES.C_NEWLINE;
        --  when P_ATTR_TYPE_NAME = REF then
        when P_ATTR_TYPE_NAME = 'TIMESTAMP' then
          V_PLSQL := V_PLSQL
                  ||'          V_SERIALIZED_VALUE := V_DOUBLE_QUOTE || TO_CHAR(' || P_ATTR_NAME || ') || V_DOUBLE_QUOTE;' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_SERIALIZED_VALUE),V_SERIALIZED_VALUE);' || YADAMU_UTILITIES.C_NEWLINE;
        when P_ATTR_TYPE_NAME = 'TIMESTAMP WITH LOCAL TIME ZONE' then
          V_PLSQL := V_PLSQL
                  ||'          V_SERIALIZED_VALUE := V_DOUBLE_QUOTE || TO_CHAR(' || P_ATTR_NAME || ') || V_DOUBLE_QUOTE;' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_SERIALIZED_VALUE),V_SERIALIZED_VALUE);' || YADAMU_UTILITIES.C_NEWLINE;
        when P_ATTR_TYPE_NAME = 'TIMESTAMP WITH TIME ZONE' then
          V_PLSQL := V_PLSQL
                  ||'          V_SERIALIZED_VALUE := V_DOUBLE_QUOTE || TO_CHAR(' || P_ATTR_NAME || ') || V_DOUBLE_QUOTE;' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_SERIALIZED_VALUE),V_SERIALIZED_VALUE);' || YADAMU_UTILITIES.C_NEWLINE;
        when P_ATTR_TYPE_NAME = 'UROWID' then
          V_PLSQL := V_PLSQL
                  ||'          V_SERIALIZED_VALUE := TO_CHAR(' || P_ATTR_NAME || ');' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_SERIALIZED_VALUE),V_SERIALIZED_VALUE);' || YADAMU_UTILITIES.C_NEWLINE;
        when P_ATTR_TYPE_NAME = 'VARCHAR2' then
          V_PLSQL := V_PLSQL
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,V_DOUBLE_QUOTE);' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(' || P_ATTR_NAME || '),' || P_ATTR_NAME || ');' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,V_DOUBLE_QUOTE);' || YADAMU_UTILITIES.C_NEWLINE;
        when P_ATTR_TYPE_NAME = 'VARCHAR'  then
          V_PLSQL := V_PLSQL
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,V_DOUBLE_QUOTE);' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(' || P_ATTR_NAME || '),' || P_ATTR_NAME || ');' || YADAMU_UTILITIES.C_NEWLINE
                  ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,V_DOUBLE_QUOTE);' || YADAMU_UTILITIES.C_NEWLINE;
        else
          DBMS_OUTPUT.PUT_LINE('Unsupported Type: "' || P_ATTR_TYPE_NAME || '".');
      end case;
    when P_ATTR_TYPE_MOD = 'REF' then
	  -- The serialzied form needs to include the decode function eg "HEXTOREF(' || REF_VALUE || ')" to that it will be correctly deserialized 
	  V_PLSQL := V_PLSQL
              ||'          -- V_SERIALIZED_VALUE := REFTOHEX(' || P_ATTR_NAME || ');' || YADAMU_UTILITIES.C_NEWLINE
		      ||'          select V_DOUBLE_QUOTE || REFTOHEX(' || P_ATTR_NAME || ') || V_DOUBLE_QUOTE into V_SERIALIZED_VALUE from dual;' || YADAMU_UTILITIES.C_NEWLINE
              ||'          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_SERIALIZED_VALUE),V_SERIALIZED_VALUE);' || YADAMU_UTILITIES.C_NEWLINE;
    when P_ATTR_TYPE_OWNER is not NULL then
      V_TYPECODE := extendTypeList(P_TYPE_LIST, P_ATTR_TYPE_OWNER, P_ATTR_TYPE_NAME);
      case
        when V_TYPECODE = 'COLLECTION' then
          V_PLSQL := V_PLSQL
                  ||'          V_ANYDATA := ANYDATA.convertCollection(' || P_ATTR_NAME || ');' || YADAMU_UTILITIES.C_NEWLINE;
        when V_TYPECODE = 'OBJECT' then
          V_PLSQL := V_PLSQL
                  ||'          V_ANYDATA := ANYDATA.convertObject(' || P_ATTR_NAME || ');' || YADAMU_UTILITIES.C_NEWLINE;
      end case;
      V_PLSQL := V_PLSQL
              || '          serialize_Object(P_TABLE_OWNER,V_ANYDATA,P_SERIALIZATION);' || YADAMU_UTILITIES.C_NEWLINE;
    else
      V_PLSQL := V_PLSQL
              ||'           V_SERIALIZED_VALUE := V_DOUBLE_QUOTE || TO_CHAR(' || P_ATTR_NAME || ') || V_DOUBLE_QUOTE;' || YADAMU_UTILITIES.C_NEWLINE
              ||'           DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,length(V_SERIALIZED_VALUE),V_SERIALIZED_VALUE);' || YADAMU_UTILITIES.C_NEWLINE;
  end case;

  return V_PLSQL;

end;

function serializeType(P_TYPE_RECORD TYPE_LIST,P_TYPE_LIST IN OUT NOCOPY TYPE_LIST_TABLE)
return CLOB
/*
**
** Returns a PL/SQL 'when' block that will serialize a type using the attribute names.
**
** Assumes that the type to be serialized is in an PL/SQL ANYDATA Variable called P_ANYDATA
** and the serialized content will be wriite to PL/SQL CLOB Variable called P_SERIALIZATION
**
**/
as
  V_PLSQL_BLOCK      CLOB;
  V_PLSQL            VARCHAR2(32767);

  cursor getAttributes
  is
  select ATTR_NAME, ATTR_TYPE_OWNER, ATTR_TYPE_NAME, ATTR_TYPE_MOD, ATTR_NO
    from ALL_TYPE_ATTRS
   where OWNER = P_TYPE_RECORD.OWNER
     and TYPE_NAME = P_TYPE_RECORD.TYPE_NAME
   order by ATTR_NO;

  cursor getCollectionTypeInfo
  is
  select ELEM_TYPE_OWNER, ELEM_TYPE_NAME, ELEM_TYPE_MOD
    from ALL_COLL_TYPES
   where OWNER = P_TYPE_RECORD.OWNER
     and TYPE_NAME = P_TYPE_RECORD.TYPE_NAME;

  V_TYPE_NAME VARCHAR2(257);

begin
  $IF $$DEBUG $THEN DBMS_OUTPUT.PUT_LINE('serializeType() : Processing Type: "' || P_TYPE_RECORD.OWNER || '"."' || P_TYPE_RECORD.TYPE_NAME || '".'); $END
  DBMS_LOB.CREATETEMPORARY(V_PLSQL_BLOCK,TRUE,DBMS_LOB.CALL);


  V_PLSQL := '    when V_TYPE_INFO.OWNER = ''' || P_TYPE_RECORD.OWNER || ''' and V_TYPE_INFO.TYPE_NAME = ''' || P_TYPE_RECORD.TYPE_NAME || ''' then' || YADAMU_UTILITIES.C_NEWLINE
          || '      declare' || YADAMU_UTILITIES.C_NEWLINE
          || '        V_OBJECT           "' || P_TYPE_RECORD.OWNER || '"."' || P_TYPE_RECORD.TYPE_NAME || '";' || YADAMU_UTILITIES.C_NEWLINE
          || '      begin' || YADAMU_UTILITIES.C_NEWLINE;

  if (P_TYPE_RECORD.TYPECODE = 'OBJECT') then
    V_PLSQL := V_PLSQL
            || '        V_RESULT := P_ANYDATA.getObject(V_OBJECT);' || YADAMU_UTILITIES.C_NEWLINE;
  else
    V_PLSQL := V_PLSQL
            || '        V_RESULT := P_ANYDATA.getCollection(V_OBJECT);' || YADAMU_UTILITIES.C_NEWLINE;
  end if;

  V_PLSQL := V_PLSQL
          || '        if (V_OBJECT is NULL) then' || YADAMU_UTILITIES.C_NEWLINE
          || '          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,4,''NULL'');' || YADAMU_UTILITIES.C_NEWLINE
          || '          return;' || YADAMU_UTILITIES.C_NEWLINE
          || '        end if; ' || YADAMU_UTILITIES.C_NEWLINE
		  || '        if (P_TABLE_OWNER = ''' || P_TYPE_RECORD.OWNER || ''') then ' || YADAMU_UTILITIES.C_NEWLINE
		  || '   	    V_OBJECT_CONSTRUCTOR := ''"'|| P_TYPE_RECORD.TYPE_NAME || '"('';' || YADAMU_UTILITIES.C_NEWLINE
		  || '        else' || YADAMU_UTILITIES.C_NEWLINE
		  || '   	    V_OBJECT_CONSTRUCTOR := ''"' || P_TYPE_RECORD.OWNER || '"."' || P_TYPE_RECORD.TYPE_NAME || '"('';' || YADAMU_UTILITIES.C_NEWLINE
		  || '        end if;' ||YADAMU_UTILITIES.C_NEWLINE;
		  
  if P_TYPE_RECORD.TYPECODE = 'OBJECT' then

    V_PLSQL := V_PLSQL
            || '        DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,''{'');' || YADAMU_UTILITIES.C_NEWLINE;
			
	if (C_TYPED_JSON) then
	  V_TYPE_NAME := P_TYPE_RECORD.OWNER || '.' || P_TYPE_RECORD.TYPE_NAME;
      V_PLSQL := V_PLSQL
              || '        DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,' || (length(V_TYPE_NAME) + 13) || ',''"o:type": "' || V_TYPE_NAME || '",'');' || YADAMU_UTILITIES.C_NEWLINE;		
	end if;

    DBMS_LOB.WRITEAPPEND(V_PLSQL_BLOCK,length(V_PLSQL),V_PLSQL);

    for a in getAttributes loop

      $IF $$DEBUG $THEN DBMS_OUTPUT.PUT_LINE('serializeType() : Processing Attribute: ' || a.ATTR_NAME || '. Data Type: ' || a.ATTR_TYPE_NAME); $END

      V_PLSQL := serializeAttr('V_OBJECT."' || a.ATTR_NAME || '"', a.ATTR_TYPE_OWNER, a.ATTR_TYPE_NAME, a.ATTR_TYPE_MOD, P_TYPE_LIST);

      V_PLSQL := V_PLSQL
              || '        end if;' || YADAMU_UTILITIES.C_NEWLINE;

      if (a.ATTR_NO <  P_TYPE_RECORD.ATTR_COUNT) then
        V_PLSQL := V_PLSQL
                ||'         DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,'','');' || YADAMU_UTILITIES.C_NEWLINE;
      else
        V_PLSQL := V_PLSQL
                || '        DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,''}'');' || YADAMU_UTILITIES.C_NEWLINE;
      end if;

      DBMS_LOB.WRITEAPPEND(V_PLSQL_BLOCK,length(V_PLSQL),V_PLSQL);

    end loop;
  else

    V_PLSQL := V_PLSQL
            || '        DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,''['');' || YADAMU_UTILITIES.C_NEWLINE;

    DBMS_LOB.WRITEAPPEND(V_PLSQL_BLOCK,length(V_PLSQL),V_PLSQL);

    for c in getCollectionTypeInfo loop
      V_PLSQL := '        for IDX in 1..V_OBJECT.count loop' || YADAMU_UTILITIES.C_NEWLINE
              || serializeAttr('V_OBJECT(IDX)',c.ELEM_TYPE_OWNER,c.ELEM_TYPE_NAME,c.ELEM_TYPE_MOD,P_TYPE_LIST) || YADAMU_UTILITIES.C_NEWLINE
              || '        end if;' || YADAMU_UTILITIES.C_NEWLINE
              || '        if (IDX < V_OBJECT.count) then ' || YADAMU_UTILITIES.C_NEWLINE
              || '          DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,'','');' || YADAMU_UTILITIES.C_NEWLINE
              || '        end if;' || YADAMU_UTILITIES.C_NEWLINE
              || '        end loop;' || YADAMU_UTILITIES.C_NEWLINE
              || '        DBMS_LOB.WRITEAPPEND(P_SERIALIZATION,1,'']'');' || YADAMU_UTILITIES.C_NEWLINE;
    end loop;
    DBMS_LOB.WRITEAPPEND(V_PLSQL_BLOCK,length(V_PLSQL),V_PLSQL);
  end if;

  V_PLSQL := '      end;' || YADAMU_UTILITIES.C_NEWLINE;
  DBMS_LOB.WRITEAPPEND(V_PLSQL_BLOCK,length(V_PLSQL),V_PLSQL);

  return V_PLSQL_BLOCK;

end;
--
function serializeTypes(P_TYPE_LIST IN OUT NOCOPY TYPE_LIST_TABLE)
return CLOB
as
--
  V_PLSQL_BLOCK    CLOB;
  V_CASE_BLOCK     CLOB;
  V_SQL_FRAGMENT   VARCHAR2(32767);
  V_ANYTYPE        ANYTYPE;
  V_IDX            PLS_INTEGER := 1;

begin
  DBMS_LOB.CREATETEMPORARY(V_PLSQL_BLOCK,TRUE,DBMS_LOB.CALL);
  DBMS_LOB.WRITEAPPEND(V_PLSQL_BLOCK,length(CODE_SERIALIZE_OBJECT_PART1),CODE_SERIALIZE_OBJECT_PART1);
  
  $IF $$DEBUG $THEN DBMS_OUTPUT.PUT_LINE('OBJECT_SERIALIZATION.serializeTypes(): Type count = ' || P_TYPE_LIST.count); $END

  if (P_TYPE_LIST.count = 0) then
    return NULL;
  end if;

  loop
    $IF $$DEBUG $THEN DBMS_OUTPUT.PUT_LINE('OBJECT_SERIALIZATION.serializeTypes() : Processing[' || V_IDX || '].'); $END
    V_CASE_BLOCK := serializeType(P_TYPE_LIST(V_IDX),P_TYPE_LIST);
    DBMS_LOB.APPEND(V_PLSQL_BLOCK,V_CASE_BLOCK);
    DBMS_LOB.FREETEMPORARY(V_CASE_BLOCK);
    exit when (V_IDX = P_TYPE_LIST.count);
    V_IDX := V_IDX + 1;
  end loop;

  V_SQL_FRAGMENT := '  end case;' || YADAMU_UTILITIES.C_NEWLINE
                 || 'end;' || YADAMU_UTILITIES.C_NEWLINE;

  DBMS_LOB.writeAppend(V_PLSQL_BLOCK,length(V_SQL_FRAGMENT),V_SQL_FRAGMENT);

  DBMS_LOB.writeAppend(V_PLSQL_BLOCK,length(CODE_SERIALIZE_OBJECT),CODE_SERIALIZE_OBJECT);

  return V_PLSQL_BLOCK;

end;
--
function SERIALIZE_TYPE(P_TYPE_OWNER VARCHAR2, P_TYPE_NAME VARCHAR2)
return CLOB
as
  V_TYPE_LIST TYPE_LIST_TABLE;
begin
  --
  $IF DBMS_DB_VERSION.VER_LE_11_2 $THEN
  select TYPE_LIST(OWNER, TYPE_NAME, ATTRIBUTES, TYPECODE)
  --
  $ELSE
  --
  select OWNER, TYPE_NAME, ATTRIBUTES, TYPECODE
  $END
  --
    bulk collect into V_TYPE_LIST
    from (
	  select distinct OWNER, TYPE_NAME, ATTRIBUTES, TYPECODE
        from ALL_TYPES
       start with OWNER = P_TYPE_OWNER and TYPE_NAME = P_TYPE_NAME
             connect by prior TYPE_NAME = SUPERTYPE_NAME
                 and prior OWNER = SUPERTYPE_OWNER
	);
	
  return serializeTypes(V_TYPE_LIST);
end;
--
function SERIALIZE_TABLE_TYPES(P_TABLE_OWNER VARCHAR2, P_TABLE_NAME VARCHAR2)
return CLOB
as
  V_TYPE_LIST TYPE_LIST_TABLE;
begin
  --
  $IF DBMS_DB_VERSION.VER_LE_11_2 $THEN
  select TYPE_LIST(OWNER, TYPE_NAME, ATTRIBUTES, TYPECODE)
  --
  $ELSE
  --
  select OWNER, TYPE_NAME, ATTRIBUTES, TYPECODE
  $END
  --
    bulk collect into V_TYPE_LIST
	from (
      select distinct OWNER, TYPE_NAME, ATTRIBUTES, TYPECODE
        from ALL_TYPES at, (
          select distinct DATA_TYPE_OWNER,  DATA_TYPE
            from ALL_TAB_COLS atc
           where atc.DATA_TYPE_OWNER is not NULL
             and atc.DATA_TYPE not in ('RAW','XMLTYPE','ANYDATA')
	         and ((HIDDEN_COLUMN = 'NO') or (COLUMN_NAME = 'SYS_NC_ROWINFO$'))
             and atc.OWNER = P_TABLE_OWNER
             and atc.TABLE_NAME = P_TABLE_NAME
        ) tlt
        start with at.TYPE_NAME = tlt.DATA_TYPE
          and at.OWNER = tlt.DATA_TYPE_OWNER
              connect by prior at.TYPE_NAME = SUPERTYPE_NAME
                  and prior at.OWNER = SUPERTYPE_OWNER
	);
	
  return serializeTypes(V_TYPE_LIST);
end;
--
function SERIALIZE_TABLE_TYPES(P_TABLE_LIST TABLE_INFO_TABLE)
/*
**
** Fetch the Type Heirachy of all types that are used by the columns in the table.
**
*/
return CLOB
as
  V_TYPE_LIST TYPE_LIST_TABLE;
$IF DBMS_DB_VERSION.VER_LE_11_2 $THEN
  -- Cannot use local types in SQL in 11.2.x
  V_TABLE_LIST TABLE_INFO_TABLE := TABLE_INFO_TABLE();
begin
  V_TABLE_LIST.extend(P_TABLE_LIST.count);
  for i in P_TABLE_LIST.first .. P_TABLE_LIST.last loop
     V_TABLE_LIST(i) := TABLE_INFO_RECORD(P_TABLE_LIST(i).OWNER,P_TABLE_LIST(i).TABLE_NAME);
  end loop;
$ELSE
  V_TABLE_LIST TABLE_INFO_TABLE := P_TABLE_LIST;
begin
$END  
  --
  $IF DBMS_DB_VERSION.VER_LE_11_2 $THEN
  select TYPE_LIST(OWNER, TYPE_NAME, ATTRIBUTES, TYPECODE)
  --
  $ELSE
  --
  select OWNER, TYPE_NAME, ATTRIBUTES, TYPECODE
  $END
  --
    bulk collect into V_TYPE_LIST
    from (
	    select distinct OWNER, TYPE_NAME, ATTRIBUTES, TYPECODE
          from ALL_TYPES at, (
            select distinct DATA_TYPE_OWNER,  DATA_TYPE
              from ALL_TAB_COLS atc, TABLE(V_TABLE_LIST) tl
             where atc.DATA_TYPE_OWNER is not NULL
               and atc.DATA_TYPE not in ('RAW','XMLTYPE','ANYDATA')
	           and ((HIDDEN_COLUMN = 'NO') or (COLUMN_NAME = 'SYS_NC_ROWINFO$'))
               and atc.OWNER = tl.OWNER
               and atc.TABLE_NAME = tl.TABLE_NAME
          ) tlt
      start with at.TYPE_NAME = tlt.DATA_TYPE
        and at.OWNER = tlt.DATA_TYPE_OWNER
            connect by prior at.TYPE_NAME = SUPERTYPE_NAME
                and prior at.OWNER = SUPERTYPE_OWNER
	);
	
  return serializeTypes(V_TYPE_LIST);
end;
--
function DESERIALIZE_TYPE(P_TYPE_OWNER VARCHAR2, P_TYPE_NAME VARCHAR2) 
return VARCHAR2 
as
  V_TYPE_REFERENCE VARCHAR2(266);
  V_SQL_FRAGMENT   VARCHAR2(4000);
begin
    V_TYPE_REFERENCE := '"' || P_TYPE_OWNER || '"."' || P_TYPE_NAME || '"';

	V_SQL_FRAGMENT := 'function "#' || P_TYPE_NAME || '"(P_SERIALIZATION CLOB)' || YADAMU_UTILITIES.C_NEWLINE
	               || 'return ' ||  V_TYPE_REFERENCE || YADAMU_UTILITIES.C_NEWLINE
				   || 'as' || YADAMU_UTILITIES.C_NEWLINE
				   || '   V_OBJECT ' || V_TYPE_REFERENCE ||';' || YADAMU_UTILITIES.C_NEWLINE
				   || 'begin' || YADAMU_UTILITIES.C_NEWLINE
				   || '  if (P_SERIALIZATION is NULL) then return NULL; end if;' || YADAMU_UTILITIES.C_NEWLINE  
                   || '  if (DBMS_LOB.SUBSTR(P_SERIALIZATION,17,1) = ''SERIALIZE_OBJECT:'') then return NULL; end if;	'|| YADAMU_UTILITIES.C_NEWLINE			   
				   || '  EXECUTE IMMEDIATE ''SELECT '' || P_SERIALIZATION || '' FROM DUAL'' into V_OBJECT;' || YADAMU_UTILITIES.C_NEWLINE
				   || '  return V_OBJECT;' || YADAMU_UTILITIES.C_NEWLINE
				   || 'end;' || YADAMU_UTILITIES.C_NEWLINE;
                   
  return V_SQL_FRAGMENT;
end;
--
procedure DESERIALIZE_TYPE(P_TYPE_OWNER VARCHAR2, P_TYPE_NAME VARCHAR2, P_PLSQL_BLOCK IN OUT NOCOPY CLOB)
as
  V_SQL_FRAGMENT   VARCHAR2(4000);
begin
  V_SQL_FRAGMENT := DESERIALIZE_TYPE(P_TYPE_OWNER,P_TYPE_NAME);
  DBMS_LOB.WRITEAPPEND(P_PLSQL_BLOCK,length(V_SQL_FRAGMENT),V_SQL_FRAGMENT);
end;
--
procedure DESERIALIZE_TYPES(P_TYPE_LIST IN OUT NOCOPY TYPE_LIST_TABLE, P_PLSQL_BLOCK IN OUT NOCOPY CLOB)
as
--
  V_SQL_FRAGMENT   VARCHAR2(4000);
  V_IDX            PLS_INTEGER := 1;
begin

  $IF $$DEBUG $THEN DBMS_OUTPUT.PUT_LINE('OBJECT_SERIALIZATION.DESERIALIZE_TYPES(): Type count = ' || P_TYPE_LIST.count); $END
 
  for V_IDX in 1 .. P_TYPE_LIST.count loop
    V_SQL_FRAGMENT := DESERIALIZE_TYPE(P_TYPE_LIST(V_IDX).OWNER,P_TYPE_LIST(V_IDX).TYPE_NAME);			
    DBMS_LOB.WRITEAPPEND(P_PLSQL_BLOCK,length(V_SQL_FRAGMENT),V_SQL_FRAGMENT);
  end loop;

  $IF $$DEBUG $THEN DBMS_OUTPUT.PUT_LINE('OBJECT_SERIALIZATION.DESERIALIZE_TYPES(): Size = ' || DBMS_LOB.GETLENGTH(P_PLSQL_BLOCK)); $END

end;
--
function DESERIALIZE_TYPES(P_TYPE_LIST IN OUT NOCOPY TYPE_LIST_TABLE)
return CLOB
as
  V_PLSQL_BLOCK    CLOB;
begin
  DBMS_LOB.CREATETEMPORARY(V_PLSQL_BLOCK,TRUE,DBMS_LOB.CALL);
  DESERIALIZE_TYPES(P_TYPE_LIST, V_PLSQL_BLOCK);
  return V_PLSQL_BLOCK;
end;
--
procedure DESERIALIZE_TABLE_TYPES(P_TABLE_OWNER VARCHAR2,P_TABLE_NAME VARCHAR2, P_PLSQL_BLOCK IN OUT NOCOPY CLOB)
as
  V_TYPE_LIST TYPE_LIST_TABLE;
begin
  --
  $IF DBMS_DB_VERSION.VER_LE_11_2 $THEN
  select TYPE_LIST(DATA_TYPE_OWNER, DATA_TYPE, NULL, NULL)
  --
  $ELSE
  --
  select DATA_TYPE_OWNER, DATA_TYPE, NULL, NULL
  $END
  --
    bulk collect into V_TYPE_LIST
	from (
      select distinct DATA_TYPE_OWNER, DATA_TYPE, NULL, NULL
        from ALL_TAB_COLS atc
       where atc.DATA_TYPE_OWNER is not NULL
         and atc.DATA_TYPE not in ('RAW','XMLTYPE','ANYDATA')
         and ((HIDDEN_COLUMN = 'NO') or (COLUMN_NAME = 'SYS_NC_ROWINFO$'))
         and atc.OWNER = P_TABLE_OWNER
         and atc.TABLE_NAME = P_TABLE_NAME
	);
	
  DESERIALIZE_TYPES(V_TYPE_LIST, P_PLSQL_BLOCK);
end;
--
function DESERIALIZE_TABLE_TYPES(P_TABLE_OWNER VARCHAR2,P_TABLE_NAME VARCHAR2)
return CLOB
as
  V_PLSQL_BLOCK    CLOB;
begin
  DBMS_LOB.CREATETEMPORARY(V_PLSQL_BLOCK,TRUE,DBMS_LOB.CALL);
  DESERIALIZE_TABLE_TYPES(P_TABLE_OWNER, P_TABLE_NAME, V_PLSQL_BLOCK);
  return V_PLSQL_BLOCK;
end;
--
procedure DESERIALIZE_TABLE_TYPES(P_TABLE_LIST TABLE_INFO_TABLE,P_PLSQL_BLOCK IN OUT NOCOPY CLOB)
as
  V_TYPE_LIST TYPE_LIST_TABLE;
$IF DBMS_DB_VERSION.VER_LE_11_2 $THEN
  -- Cannot use local types in SQL in 11.2.x
  V_TABLE_LIST TABLE_INFO_TABLE := TABLE_INFO_TABLE();
begin
  V_TABLE_LIST.extend(P_TABLE_LIST.count);
  for i in P_TABLE_LIST.first .. P_TABLE_LIST.last loop
     V_TABLE_LIST(i) := TABLE_INFO_RECORD(P_TABLE_LIST(i).OWNER,P_TABLE_LIST(i).TABLE_NAME);
  end loop;
$ELSE
  V_TABLE_LIST TABLE_INFO_TABLE := P_TABLE_LIST;
begin
$END  
  --
  $IF DBMS_DB_VERSION.VER_LE_11_2 $THEN
  select TYPE_LIST(DATA_TYPE_OWNER, DATA_TYPE, NULL, NULL)
  --
  $ELSE
  --
  select DATA_TYPE_OWNER, DATA_TYPE, NULL, NULL
  $END
  --
    bulk collect into V_TYPE_LIST
    from (
	  select distinct DATA_TYPE_OWNER, DATA_TYPE, NULL, NULL
        from ALL_TAB_COLS atc, TABLE(V_TABLE_LIST) tl
       where atc.DATA_TYPE_OWNER is not NULL
         and atc.DATA_TYPE not in ('RAW','XMLTYPE','ANYDATA')
         and ((HIDDEN_COLUMN = 'NO') or (COLUMN_NAME = 'SYS_NC_ROWINFO$'))
         and atc.OWNER = tl.OWNER
         and atc.TABLE_NAME = tl.TABLE_NAME
	);
		 
  DESERIALIZE_TYPES(V_TYPE_LIST, P_PLSQL_BLOCK);
end;
--
function DESERIALIZE_TABLE_TYPES(P_TABLE_LIST TABLE_INFO_TABLE)
return CLOB
as
  V_PLSQL_BLOCK    CLOB;
begin
  DBMS_LOB.CREATETEMPORARY(V_PLSQL_BLOCK,TRUE,DBMS_LOB.CALL);
  DESERIALIZE_TABLE_TYPES(P_TABLE_LIST,V_PLSQL_BLOCK);
  return V_PLSQL_BLOCK;
end;
--
end;
/
--
set TERMOUT ON
--
show errors
--
@@SET_TERMOUT
--